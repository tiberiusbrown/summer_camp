import sprites;
import globals;

import puzzles_easy;
import puzzles_hard;

char[] prog&[3] prog TITLE_STRINGS = {
    "Easy 12x8", "Hard 12x8", "Help"
};

void frame_title()
{
    $draw_sprite(0, 0, TENT, 0);

    if(title_page == 0)
    {
        if($just_pressed(A_BUTTON) && $millis() > 1000)
        {
            if($load())
            {
                title_page = 1;
                if(puzzle_type == PUZZLE_HARD) title_page = 2;
            }
            else
            {
                $init_random_seed();
                for(u8 i = 0; i < len(puzzle_index); ++i)
                    puzzle_index[i] = $random() & 1023;
                puzzle_type = PUZZLE_EASY;
                title_page = 3;
            }
        }
    }
    else
    {
        if($just_pressed(A_BUTTON))
        {
            if(title_page == 3)
            {
                state = STATE_HELP;
                help_page = 0;
            }
            else
            {
                state = STATE_GAME;
                puzzle_type = title_page - 1;
                load_next_puzzle();
            }
        }
        if($just_pressed(LEFT_BUTTON ) && --title_page < 1) title_page = 3;
        if($just_pressed(RIGHT_BUTTON) && ++title_page > 3) title_page = 1;

        constexpr u8 W = 76;

        $draw_filled_rect(64 - W / 2, 37, W, 10, LIGHT_GRAY);
        $draw_rect(63 - W / 2, 36, W + 2, 12, BLACK);
        $draw_sprite(65 - W / 2, 39, ARROWS, 0);
        $draw_sprite(64 - W / 2 + W - 5, 39, ARROWS, 1);

        char[] prog& str = TITLE_STRINGS[title_page - 1];
        $set_text_font(TITLE_FONT);
        $set_text_color(BLACK);
        u8 w = $text_width(str);
        $draw_text(64 - w / 2, 45, str);
    }
}
